#!/usr/bin/env perl6
use v6;

use Yapsi;

my @TARGETS = <run sic>;

if @*ARGS && @*ARGS[0] eq '--version' {
    my $revision = qx[git describe];
    say "This is Yapsi, revision $revision";
    shift @*ARGS;
    unless @*ARGS {
        push @*ARGS, '-e', '';
    }
}

my $target = 'run';
if @*ARGS && @*ARGS[0] ~~ /'--target='(\w+)/ {
    die "Recognized targets: {@TARGETS}"
        unless $0.lc eq any @TARGETS;
    $target = $0.lc;
    shift @*ARGS;
}

my $program;
if @*ARGS == 2 && @*ARGS[0] eq '-e' {
    $program = @*ARGS[1];
}
elsif @*ARGS == 1 {
    $program = slurp @*ARGS[0];
}
else {
    die "Usage: `$*PROGRAM_NAME <file>` or `$*PROGRAM_NAME -e '<program>'`";
}

try {
    my @sic;
    given Yapsi::Compiler.new {
        @sic = .compile($program);
        warn $_ for .warnings;
    }

    if $target eq 'sic' {
        .say for @sic;
    }
    else {
        my Yapsi::Runtime $runtime .= new;
        $runtime.run(@sic);
    }
}
say $! if $!;
