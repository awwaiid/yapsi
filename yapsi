#!/usr/bin/alpha
use v6;

use Yapsi;

my @TARGETS = <run sic>;

my $target = 'run';
if @*ARGS && @*ARGS[0] ~~ /'--target='(\w+)/ {
    die "Recognized targets: {@TARGETS}"
        unless $0.lc eq any @TARGETS;
    $target = $0.lc;
    shift @*ARGS;
}

my $program;
if @*ARGS == 2 && @*ARGS[0] eq '-e' {
    $program = @*ARGS[1];
}
elsif @*ARGS == 1 {
    $program = slurp @*ARGS[0];
}
else {
    die "Usage: `$*PROGRAM_NAME <file>` or `$*PROGRAM_NAME -e '<program>'`";
}

try {
    my Yapsi::Compiler $compiler .= new;

    my @sic = $compiler.compile($program);
    warn $_ for $compiler.warnings;

    if $target eq 'sic' {
        .say for @sic;
    }
    else {
        my Yapsi::Runtime $runtime .= new;
        $runtime.run(@sic);
    }
}
say $! if $!;
